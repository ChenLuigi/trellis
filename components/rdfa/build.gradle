apply plugin: 'java-library'
apply plugin: 'osgi'

description = 'Trellis RDFa Module'

ext {
    moduleName = 'org.trellisldp.rdfa'
}

dependencies {
    api("org.glassfish.hk2.external:javax.inject:$javaxInjectVersion")
    api project(':trellis-api')

    implementation("com.github.spullara.mustache.java:compiler:$mustacheVersion")
    implementation("org.apache.commons:commons-compress:$commonsCompressVersion")
    implementation("org.apache.jena:jena-osgi:$jenaVersion")
    implementation("org.apache.tamaya:tamaya-api:$tamayaVersion")
    implementation project(':trellis-vocabulary')

    testImplementation("org.apache.commons:commons-rdf-jena:$commonsRdfVersion")
    testImplementation("org.apache.tamaya:tamaya-core:$tamayaVersion")
    testImplementation("org.mockito:mockito-core:$mockitoVersion")
}

jar {
    manifest {
        description project.description
        docURL project.docURL
        vendor project.vendor
        license project.license

        instruction 'Automatic-Module-Name', moduleName
        instruction 'Import-Package', '*'
        instruction 'Export-Package', "${moduleName};version=${projectOsgiVersion}"
        instruction 'Require-Capability', '''\
            osgi.extender;
                filter:="(osgi.extender=osgi.serviceloader.registrar)";
                resolution:=optional,
            osgi.extender;
                filter:="(osgi.extender=osgi.serviceloader.processor)",
            osgi.serviceloader;
                filter:="(osgi.serviceloader=org.trellisldp.api.NamespaceService)";
                resolution:=optional'''
        instruction 'Provide-Capability', '''\
            osgi.serviceloader;
                osgi.serviceloader=org.trellisldp.api.RDFaWriterService'''
    }
}

